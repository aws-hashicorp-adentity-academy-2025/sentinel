import "json"
import "tfplan"

forbidden_actions = [
  "*",
  "iam:*",
  "iam:Create*",
  "iam:Delete*",
  "s3:*",
  "s3:PutObject",
  "s3:DeleteObject",
]

all_policy_resources = func() {
    policies = []

    # 루트 모듈의 aws_iam_policy
    root_resources = values(tfplan.resources.aws_iam_policy) else []
    for root_resources as _, r {
        policies += values(r)
    }

    # 서브 모듈의 aws_iam_policy
    for tfplan.module_paths as path {
        resources = values(tfplan.module(path).resources.aws_iam_policy) else []
        for resources as _, r {
            policies += values(r)
        }
    }

    return policies
}

# get all IAM Policy statements
policy_statements = func() {
    statements = []
    for all_policy_resources() as r {
        statements += json.unmarshal(r.applied.policy).Statement
    }
    return statements
}

valid_statement = func(s,a) {
    if s.Action contains a {
        print("허용되지 않는 action 발견: action=", a, ", statement=", s)   
        return false
    }
  return true
}
 
# 허용하지 않는 action이 있습니다. 확인하세요.
main = rule {
    all policy_statements() as s {
        all forbidden_actions as a {
            valid_statement(s, a)
        }
    }
}